# Maintainer: KunoiSayami <i@leanhe.dev>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Gavin Lloyd <gavinhungry@gmail.com>

pkgname=acme.sh-letsencrypt
pkgver=3.1.0
pkgrel=1
pkgdesc='An ACME Shell script, an acme client alternative to certbot, but we use letsencrypt by default'
arch=('any')
url='https://github.com/acmesh-official/acme.sh'
license=('GPL3')
depends=('bash')
optdepends=('socat: for standalone mode'
            'oath-toolkit: Used to compute the OTP for some DNS providers')
source=("acme.sh-$pkgver.tar.gz::${url}/archive/refs/tags/${pkgver}.tar.gz")
sha512sums=('b43579823ac6db85a2c36ab4aa049de199b072bfa096094f7cd424a3696e9a35a91532f9c0cf7cd664f17317e89e5776f17b274e4bf46d97f034ff31c363ffb6')

provides=('acme.sh')
conflicts=('acme.sh')

package() {
  cd acme.sh-$pkgver

  install -dm755 "$pkgdir"/usr/share/acme.sh/deploy
  install -dm755 "$pkgdir"/usr/share/acme.sh/dnsapi
  install -dm755 "$pkgdir"/usr/share/acme.sh/notify
  install -dm755 "$pkgdir"/usr/bin

  sed -i 's/DEFAULT_CA=$CA_ZEROSSL/DEFAULT_CA=$CA_LETSENCRYPT_V2/g' acme.sh

  install -m755 acme.sh "$pkgdir"/usr/share/acme.sh/
  install -m755 deploy/*.sh "$pkgdir"/usr/share/acme.sh/deploy/
  install -m755 dnsapi/*.sh "$pkgdir"/usr/share/acme.sh/dnsapi/
  install -m755 notify/*.sh "$pkgdir"/usr/share/acme.sh/notify/

  ln -s /usr/share/acme.sh/acme.sh "$pkgdir"/usr/bin/acme.sh
}
