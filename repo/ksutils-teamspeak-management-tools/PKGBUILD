# Maintainer: KunoiSayami <i@leanhe.dev>

_original_name=teamspeak-management-tools
_basename=teamspeak-management-tools
pkgname=ksutils-$_basename
pkgver=5.2.3
pkgrel=2
pkgdesc='A teamspeak tools that help you manage your teamspeak server (include auto channel).'
arch=('any')
url=https://github.com/KunoiSayami/teamspeak-management-tools.rs
source=(
    $pkgname::git+https://github.com/KunoiSayami/teamspeak-management-tools.rs#tag=v$pkgver
    "$pkgname.service"
    "default.toml"
)
license=('AGPL3')
optdepends=('redis: As key-value map backend')
sha512sums=(
    'SKIP'
    '444af3701bfb593266efe8b2fa7f6e87cdca0bb3e36865de4d662acfdbc47e6baf81326cbbb424a62d4a447ab9f1f54fa6dda68b0e5d7782f237b4f2df8c6f9c'
    '9e9a47f5d05b77c4e4533e480ba12378d2460b28f3f18819e6e27060a0c9068c19cfe4732d38dfaa53a9eb39a6d501d51c4f4406d1b1e94fe711eaf46070fc8a'
)
makedepends=('cargo' 'git')
provides=("$_basename")
options=(!lto)
backup=("etc/ksutils/$_basename/default.toml")

prepare() {
    cd "$pkgname"
    git submodule update --init
}

build() {
    cd "$pkgname"
    cargo build --release --features all
}

package() {
    install -dm700 "$pkgdir"/etc/ksutils/$_basename
    install -dm755 "$pkgdir"/usr/bin
    install -dm755 "$pkgdir"/usr/lib/systemd/system

    install -m755 "$pkgname"/target/release/"$_original_name" "$pkgdir"/usr/bin/"$pkgname"
    install -m600 default.toml "$pkgdir"/etc/ksutils/$_basename/
    install -m644 "$pkgname.service" "$pkgdir"/usr/lib/systemd/system/
}
