# Maintainer: KunoiSayami <i@leanhe.dev>

_original_name=teamspeak-management-tools
_basename=teamspeak-management-tools
pkgname=ksutils-$_basename
pkgver=6.0.0
pkgrel=1
pkgdesc='A TeamSpeak tools that help you manage your TeamSpeak server (include auto channel and more).'
arch=('any')
url=https://github.com/KunoiSayami/teamspeak-management-tools.rs
source=(
    $pkgname::git+https://github.com/KunoiSayami/teamspeak-management-tools.rs#tag=v$pkgver
    "$pkgname.service"
    "default.toml"
)
license=('AGPL3')
optdepends=('redis: As key-value map backend')
sha512sums=(
    'SKIP'
    '444af3701bfb593266efe8b2fa7f6e87cdca0bb3e36865de4d662acfdbc47e6baf81326cbbb424a62d4a447ab9f1f54fa6dda68b0e5d7782f237b4f2df8c6f9c'
    '8bc97d37e33ae9154517cae64fc425a65dc7d9f033cbd61e45b8c34c26c559cd409d15c82a7613e6bb1517589bc7c2025dc7d3395ae970750282715eea83918e'
)
makedepends=('cargo' 'git')
provides=("$_basename")
options=(!lto)
backup=("etc/ksutils/$_basename/default.toml")

prepare() {
    cd "$pkgname"
    git submodule update --init
}

build() {
    cd "$pkgname"
    cargo build --release --features all
}

package() {
    install -dm700 "$pkgdir"/etc/ksutils/$_basename
    install -dm755 "$pkgdir"/usr/bin
    install -dm755 "$pkgdir"/usr/lib/systemd/system

    install -m755 "$pkgname"/target/release/"$_original_name" "$pkgdir"/usr/bin/"$pkgname"
    install -m600 default.toml "$pkgdir"/etc/ksutils/$_basename/
    install -m644 "$pkgname.service" "$pkgdir"/usr/lib/systemd/system/
}
