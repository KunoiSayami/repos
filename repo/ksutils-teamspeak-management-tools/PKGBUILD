# Maintainer: KunoiSayami <i@leanhe.dev>

_original_name=teamspeak-management-tools
_basename=teamspeak-management-tools
pkgname=ksutils-$_basename
pkgver=5.0.1
pkgrel=1
pkgdesc='A teamspeak tools that help you manage your teamspeak server (include auto channel).'
arch=('any')
url=https://github.com/KunoiSayami/teamspeak-management-tools.rs
source=(
    $pkgname::git+https://github.com/KunoiSayami/teamspeak-management-tools.rs#tag=v$pkgver
    "$pkgname.service"
    "default.toml"
)
license=('AGPL3')
sha512sums=(
    'SKIP'
    '444af3701bfb593266efe8b2fa7f6e87cdca0bb3e36865de4d662acfdbc47e6baf81326cbbb424a62d4a447ab9f1f54fa6dda68b0e5d7782f237b4f2df8c6f9c'
    '44b66d2bf20465d761eb603679484802cf12a6d42cba20c95466680ccac042b860c1c5d772eff97ba82953879d812df67ab2d4771ae00bc9792557a6f62698f4'
)
makedepends=('cargo' 'git')
depends=('redis')
provides=("$_basename")
options=(!lto)
backup=("etc/ksutils/$_basename/default.toml")

prepare() {
    cd "$pkgname"
    git submodule update --init
}

build() {
    cd "$pkgname"
    cargo build --release
}

package() {
    install -dm700 "$pkgdir"/etc/ksutils/$_basename
    install -dm755 "$pkgdir"/usr/bin
    install -dm755 "$pkgdir"/usr/lib/systemd/system

    install -m755 "$pkgname"/target/release/"$_original_name" "$pkgdir"/usr/bin/"$pkgname"
    install -m600 default.toml "$pkgdir"/etc/ksutils/$_basename/
    install -m644 "$pkgname.service" "$pkgdir"/usr/lib/systemd/system/
}
