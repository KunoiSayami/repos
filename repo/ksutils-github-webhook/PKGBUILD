# Maintainer: KunoiSayami <i@leanhe.dev>

_original_name=github-webhook-notification
_basename=github-webhook
pkgname=ksutils-$_basename
pkgver=1.0.0
pkgrel=1
pkgdesc='A simple webhook server forward message to telegram'
arch=('any')
url=https://github.com/KunoiSayami/github-webhook-notification.rs
source=(
    $pkgname::git://github.com/KunoiSayami/github-webhook-notification.rs#tag=v$pkgver
    "$pkgname@.service"
    "default.toml")
license=('AGPL3')
sha512sums=(
    'SKIP'
    '393f3e06466e3dc6b4e550e0257279094430fab65b44c44d7c09cd6c2b26d200cd3b6aa6bc72d417c20b700a3b0f5e9a4845dddc3511d9dbaba0c3d8ff31b869'
    '8014c6784febd857a47b9f4460d537a0a967a15fa33c6b8b2ccec382125219530f1d2cd25e406b14a902088e2c1693ed68ea43d4f4daab04829129d89ff9b803')
makedepends=('cargo' 'git')
depends=('systemd')
provides=("$_basename")
options=(!lto)

build() {
    cd "$pkgname"
    cargo build --release
}

package() {
    install -dm700 "$pkgdir"/etc/ksutils/$_basename
    install -dm755 "$pkgdir"/usr/bin
    install -dm755 "$pkgdir"/usr/lib/systemd/system

    install -m755 "$pkgname"/target/release/"$_original_name" "$pkgdir"/usr/bin/"$pkgname"
    install -m600 default.toml "$pkgdir"/etc/ksutils/$_basename/
    install -m644 "$pkgname@.service" "$pkgdir"/usr/lib/systemd/system/
}
